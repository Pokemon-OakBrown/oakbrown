script EventScript_AfterWhiteOutHeal { 
	lockforce
	textcolor(1)
	msgbox(Text_FirstShouldRestoreMonsHealth)
	closemessage
	call(EventScript_PkmnCenterNurse_TakeAndHealPkmn)
	msgbox(Text_MonsHealed)
	closemessage
	applymovement(VAR_LAST_TALKED, Movement_Bow)
	waitmovement(0)
	// If the normal music is the Pok√©center, we play the net center theme after whiteout.
	// Otherwise, we'll play the map's nor,al music.
	special(QueryCurrLocationDefaultMusic)
	if (var(VAR_RESULT) == MUS_POKE_CENTER) {
		fadenewbgm(MUS_NET_CENTER)
	} else {
		fadedefaultbgm
	}
	applymovement(OBJ_EVENT_ID_PLAYER, Movement_FaceDown)
	waitmovement(0)
	releaseforce
	end
}

raw `
EventScript_AfterWhiteOutMomHeal:: @ 81A8DD8
	lockforce
	textcolor 1
	applymovement 1 Movement_FaceDown
	waitmovement 0
	msgbox Text_HadQuiteAnExperienceTakeRest
	call EventScript_OutOfCenterPartyHeal
	msgbox Text_MomExplainHPGetPotions
	fadedefaultbgm
	releaseforce
	end

EventScript_FieldPoison:: @ 81A8DFD
	lockforce
	textcolor 3
	special TryFieldPoisonWhiteOut
	waitstate
	compare VAR_RESULT, TRUE
	goto_if_eq EventScript_FieldWhiteOut
	releaseforce
	end

EventScript_FieldWhiteOut:: @ 81A8E11
	checkmoney 1, 0
	compare VAR_RESULT, FALSE
	goto_if_eq EventScript_FieldWhiteOutNoMoney
	compare VAR_RESULT, TRUE
	goto_if_eq EventScript_FieldWhiteOutHasMoney
	end

EventScript_FieldWhiteOutNoMoney:: @ 81A8E2E
	msgbox Text_WhitedOut
	goto EventScript_FieldWhiteOutFade
	end

EventScript_FieldWhiteOutHasMoney:: @ 81A8E3C
	special OverworldWhiteOutGetMoneyLoss
	msgbox Text_WhitedOutLostMoney
	goto EventScript_FieldWhiteOutFade
	end

EventScript_FieldWhiteOutFade:: @ 81A8E4D
	special Script_FadeOutMapMusic
	waitstate
	fadescreen FADE_TO_BLACK
	special SetCB2WhiteOut
	waitstate
	end
`
