mapscripts SeashoreCity_MapScripts {}

script SeashoreCity_OldMan {
    lockall
    faceplayer
    msgbox(format("You should never be afraid to follow your dreams.\p"
    "By attempting to reach your goals, things might not work out the way you expect, of course…\p"
    "… But you may end up accomplishing something else, instead!\pYou never know where you might end up!"))
    release
}

script SeashoreCity_YoungBoy {
    lockall
    faceplayer
    if (!flag(FLAG_SYS_POKEMON_GET)) {
        message(format("Hey! Are you going to become a\nPokémon trainer, {PLAYER}?"))
        waitmessage
        yesnobox(2, 2)
        if (var(VAR_RESULT)) {
            msgbox(format("Cool! I'm going to be one soon as\nwell!\pIt's very hard, but rewarding!\p"
            "Well, good luck in Gravel Town! Hope to see you again soon!"))
        } else {
            msgbox(format("Huh?! But you've been talking about getting a Pokémon nonstop since you moved in!"))
        }
    } elif (!flag(FLAG_REACHED_MERSON_CITY)) {
        msgbox(format("Hey, you got a Pokémon, didn't you?\nWhich one did you pick?"))
    } else {
        msgbox(format("Hey, {PLAYER}!\pHow's the trainer life treating you?"))
    }
    release
}

script SeashoreCity_Lass {
    lockall
    faceplayer
    if (!flag(FLAG_REACHED_MERSON_CITY)) {
        msgbox("Are those new shoes?\pTry running with them by holding {B_BUTTON}!")
    } elif (flag(FLAG_SYS_GAME_CLEAR)) {
        msgbox(format("You've travelled the entire region by now…\pAnd you're still wearing those old running shoes!"))
    } elif (flag(FLAG_GOT_BICYCLE)) {
        msgbox(format("Even though you have a Bicycle, you're still wearing those running shoes, huh?"))
    } else {
        msgbox(format("Those running shoes look dirty and worn out!\pDo you travel a lot on foot?"))
    }
    release
}

script SeashoreCity_Sign {
    msgbox(format("Seashore City\pWhere waves of new journeys begin their long travels."))
    closemessage
}
