const PLAYER_ID = OBJ_EVENT_ID_PLAYER
const BIKER_CLERK_ID = 1

const TEMPVAR_PLAYER_X = VAR_TEMP_8
const TEMPVAR_PLAYER_Y = VAR_TEMP_9
const TEMPFLAG_HIDE_BIKER = FLAG_TEMP_A
const TEMPFLAG_HIDE_CLERKS = FLAG_TEMP_B

mapscripts GravelTown_PokeMart_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_REACHED_MERSON_CITY)) {
            setflag(TEMPFLAG_HIDE_BIKER)
        } else {
            setflag(TEMPFLAG_HIDE_CLERKS)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_MAP_SCENE_GRAVEL_TOWN, 2: GravelTown_PokeMart_TimsPizza
    ]
}

script GravelTown_PokeMart_BikerClerk {
    lock
    faceplayer
    if (flag(FLAG_SYS_POKEDEX_GET)) {
        getplayerxy(TEMPVAR_PLAYER_X, TEMPVAR_PLAYER_Y)
        if (var(TEMPVAR_PLAYER_Y) == 4) {
            // Not talking from across counter
            msgbox(format("Dude, if you wanna buy something, you gotta talk to me from across the counter."))
            applymovement(BIKER_CLERK_ID, Movement_FaceRight)
            waitmovement(BIKER_CLERK_ID)
            closemessage
            release
            end
        }
        textcolor(2)
        message("Hey, man!\nWhatchu gonna buy?")
        waitmessage
        setflag(FLAG_SYS_BIKER_MART)
        call(Mart_StandardShop)
        clearflag(FLAG_SYS_BIKER_MART)
        msgbox("Aight, that's cool.\nSee ya around.")
    } else {
        msgbox(GravelTown_PokeMart_WeDontDeliver)
    }
    release
}

text GravelTown_PokeMart_WeDontDeliver {
    format("Hey, don't look at me!\nWe don't deliver, you know…")
}

script GravelTown_PokeMart_Pokefan {
    lock
    faceplayer
    msgbox(format("This store isn't very well-stocked because Gravel Town is out of the way.\p"
        "You can still get the basics, like potions and Pokéballs.\p"
        "… But where am I going to find a new toy for my Glameow?"))
    release
}

script GravelTown_PokeMart_BaldingMan {
    lock
    faceplayer
    if (flag(FLAG_REACHED_MERSON_CITY)) {
        msgbox(format("These shop clerks really know what they're doing!"))
    } else {
        msgbox(format("I'm a little concerned about that shop clerk.\pHe doesn't seem to know what he's doing."))
    }
    release
}

// Pick up Tim's Pizza
script GravelTown_PokeMart_TimsPizza {
    lockall
    textcolor(0)
    msgbox("Yo, dude!\nCome here for a second!")
    closemessage
    applymovement(PLAYER_ID, Movement_PlayerWalkToMartCounter)
    waitmovement(PLAYER_ID)
    msgbox(format("Are you a delivery guy or something?\pTim forgot to pick up his pizza!\nHere you go!"))
    textcolor(3)
    playfanfare(MUS_OBTAIN_KEY_ITEM)
    message(format("You got Tim's pizza!\nBetter deliver it!"))
    waitmessage
    waitfanfare
    additem(ITEM_TIMS_PIZZA)
    setvar(VAR_MAP_SCENE_GRAVEL_TOWN, 3)
    setflag(FLAG_GOT_TIMS_PIZZA)
    textcolor(0)
    msgbox(GravelTown_PokeMart_WeDontDeliver)
    release
}

movement Movement_PlayerWalkToMartCounter {
    walk_up * 5
    walk_left
}

script GravelTown_PokeMart_Clerk {
    call(Mart_CheckAcrossCounter)
    textcolor(2)
    message(format("Welcome to the Gravel Town Pokémart."))
    waitmessage
    call(Mart_StandardShop)
    msgbox(Mart_ThanksForShopping)
    release
}

script GravelTown_PokeMart_SpecialClerk {
    textcolor(2)
    message(format("Welcome to the Gravel Town Pokémart's specialty counter."))
    waitmessage
    pokemart(GravelTownMart)
    msgbox(Mart_ThanksForShopping)
    release
}

mart GravelTownMart {
    ITEM_MOON_STONE
    ITEM_NEST_BALL
    ITEM_MECH_MAIL
    ITEM_WOOD_MAIL
}
