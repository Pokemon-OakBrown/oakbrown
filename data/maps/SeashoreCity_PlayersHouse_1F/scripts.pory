mapscripts SeashoreCity_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(SPAWN_SEASHORE_CITY)
    }
}

script SeashoreCity_PlayersHouse_1F_TV {
    msgbox(format("There's a movie on TV called “Naljo's Guardians”.\p"
        "It appears to be based off of local mythology.\p"
        "… … … Better get going!"))
    closemessage
}

script SeashoreCity_PlayersHouse_1F_Mom {
    lock
    faceplayer
    if (!flag(FLAG_SYS_POKEMON_GET)) {
        gettime
        if (var(VAR_0x8003) == TIME_MORNING) {
            msgbox(format("Mom: Good morning, sweetie!\nToday's your big day!"))
        } else {
            msgbox(format("Mom: {PLAYER}, today's your big day! I'm so proud of you."))
        }
        msgbox(format("You should start walking south to Gravel Town soon.\pTim will be expecting you."))
    } else {
        msgbox(format("Mom: {PLAYER}, you look tired!\nYou should take a quick rest."))
        closemessage
        call(EventScript_OutOfCenterPartyHeal)
        setrespawn(SPAWN_SEASHORE_CITY)
        msgbox(format("Mom: Oh, good! You and your Pokémon are looking great!\pKnock 'em dead, sweetie!"))
    }
    release
}
