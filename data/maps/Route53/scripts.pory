mapscripts Route53_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_PROLOGUE) || flag(FLAG_SYS_POKEDEX_GET)) {
            setflag(FLAG_HIDE_MURA_ROUTE53)
        } else {
            clearflag(FLAG_HIDE_MURA_ROUTE53)
        }
    }
}

script Route53_Hiker {
    lock
    faceplayer
    msgbox(format("This road is pretty straight-forward.\pIt's impossible to get lost on this road!\p… Wait, where am I, again?"))
    release
}

script Route53_Hayden_Prologue {
    lock
    faceplayer
    msgbox(format("I caught my first Pokémon a few weeks ago. How about you?\p"
        "… Oh? You're going to Gravel Town to get your first Pokémon now?\p"
        "Cool! Once you're done, come back and then we can battle!"))
    release
}

script Route53_Clerk {
    lock
    faceplayer
    if (flag(FLAG_GOT_POTION_ON_ROUTE_53)) {
        msgbox(format("Sorry, I'm all out of free samples!\pIf you need potions, get some at any Pokémart!"))
    } else {
        msgbox(format("Hi!\pThe Gravel Town Pokémart is giving out free samples today, so go ahead and take this potion!"))
        checkitemspace(ITEM_POTION, 1)
        if (var(VAR_RESULT)) {
            textcolor(3)
            getitemname(1, ITEM_POTION)
            playfanfare(MUS_OBTAIN_ITEM)
            message(Text_ObtainedTheX)
            waitmessage
            waitfanfare
            additem(ITEM_POTION)
            setflag(FLAG_GOT_POTION_ON_ROUTE_53)
            call(EventScript_RestorePrevTextColor)
            msgbox(format("Don't hesitate to head to a Pokémart for all of your training needs!"))
        } else {
            msgbox(format("… Whoops! You don't have any room in your bag!\pHow did you manage that?!"))
        }
    }
    release
}

script Route53_Sign {
    msgbox("Route 53\nGravel Town - Seashore City")
    closemessage
}

script Route53_Hayden {
    trainerbattle_single(TRAINER_YOUNGSTER_HAYDEN, 
        "We're both trainers now!\nYou know what that means!\pAre you ready for this?", 
        "Oh no!\nYou were ready for that!")
    msgbox(format("The tall grass here is blocked by trees.\pThat means it's hard for me to train my Pokémon.\p"
        "Maybe I should try to teach my Sentret to use HM01 Cut…"), MSGBOX_AUTOCLOSE)
}

script Route53_Mura {
    lock
    faceplayer
    switch(var(VAR_STARTER_MON)) {
        case STARTER_BULBASAUR:
            getspeciesname(0, SPECIES_CHARMANDER)
        case STARTER_SQUIRTLE:
            getspeciesname(0, SPECIES_BULBASAUR)
        case STARTER_CHARMANDER:
            getspeciesname(0, SPECIES_SQUIRTLE)
    }
    msgbox(format("Mura: Lame, lame, lame!\pAll the wild Pokémon are on the other side of these trees!\p"
        "How am I supposed to get stronger if I can't train?"))
    if (flag(FLAG_BEAT_MURA_STARTER)) {
        msgbox(format("… … … Don't give me that look, {PLAYER}!\p"
            "Next time we fight, my {STR_VAR_1} will mop the floor with you!"))
    } else {
        msgbox(format("Heh heh. Guess you're out of luck, {PLAYER}.\p"
            "Because if neither of us can get stronger…\p… That means {STR_VAR_1} wins forever!"))
    }
    release
}

// -----------------------
// Meeting Tim for the first time takes place mostly in Gravel Town,
// But it starts in Route 53 so all the scripts are here.

const TIM_ID = 6
const PLAYER_ID = OBJ_EVENT_ID_PLAYER

script Route53_MeetTim_Start {
    forcelock
    playbgm(MUS_TIM, 0)

    // Figure out which trigger we are at
    getplayerxy(VAR_TEMP_0, VAR_TEMP_F)
    switch(var(VAR_TEMP_0)) {
        case 25:
            setvar(VAR_TEMP_0, 1)
        case 24:
            setvar(VAR_TEMP_0, 2)
        case 23:
            setvar(VAR_TEMP_0, 3)
        case 22:
            setvar(VAR_TEMP_0, 4)
        case 21:
            setvar(VAR_TEMP_0, 5)
    }

    // Then apply the proper movement for Tim
    switch(var(VAR_TEMP_0)) {
        case 1:
            applymovement(TIM_ID, Movement_TimStart1)
        case 2:
            applymovement(TIM_ID, Movement_TimStart2)
        case 3:
            applymovement(TIM_ID, Movement_TimStart3)
        case 4:
            applymovement(TIM_ID, Movement_TimStart4)
        case 5:
            applymovement(TIM_ID, Movement_TimStart5)
    }
    waitmovement(TIM_ID)
    textcolor(0)
    msgbox(format("There you are!\nYou're late!"))
    goto(Route53_MeetTim_Main)
}

script Route53_MeetTim_SequenceBreak {
    forcelock
    playbgm(MUS_OAK, 0)
    setvar(VAR_TEMP_0, 4)
    applymovement(TIM_ID, Movement_TimSequenceBreak)
    waitmovement(TIM_ID)
    textcolor(0)
    applymovement(PLAYER_ID, Movement_FaceRight)
    msgbox(format("There you are!\nYou're late!\pAnd…\nErm…\pYou're in someone's backyard???"))
    closemessage
    getplayerxy(VAR_TEMP_0, VAR_TEMP_F)
    if (var(VAR_TEMP_0) == 18) {
        applymovement(PLAYER_ID, Movement_PlayerUndoSequenceBreakFar)
        applymovement(TIM_ID, Movement_TimStarePlayerSequenceBreakFar)
    } else {
        applymovement(PLAYER_ID, Movement_PlayerUndoSequenceBreakNear)
        applymovement(TIM_ID, Movement_TimStarePlayerSequenceBreakNear)
    }
    waitmovement(PLAYER_ID)
    setvar(VAR_TEMP_0, 4)
    msgbox(format("A-Anyway!"))
    goto(Route53_MeetTim_Main)
}

script Route53_MeetTim_Main {
    msgbox(format("I've been waiting for you to get here!\pPlease come with me!"))
    closemessage
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    switch(var(VAR_TEMP_0)) {
        case 1:
            applymovement(TIM_ID, Movement_TimWalkToLab1)
            applymovement(PLAYER_ID, Movement_PlayerWalkToLab1)
        case 2:
            applymovement(TIM_ID, Movement_TimWalkToLab2)
            applymovement(PLAYER_ID, Movement_PlayerWalkToLab2)
        case 3:
            applymovement(TIM_ID, Movement_TimWalkToLab3)
            applymovement(PLAYER_ID, Movement_PlayerWalkToLab3)
        case 4:
            applymovement(TIM_ID, Movement_TimWalkToLab4)
            applymovement(PLAYER_ID, Movement_PlayerWalkToLab4)
        case 5:
            applymovement(TIM_ID, Movement_TimWalkToLab5)
            applymovement(PLAYER_ID, Movement_PlayerWalkToLab5)
    }
    // Tim's overworld can't be referenced properly after unloading Route 53.
    // We have to rely on the player movement and thus Tim's movement manually delays
    // for door animation before Tim enters.
    waitmovement(PLAYER_ID)
    delay(24)
    opendoor(40, 16)
	waitdooranim
    applymovement(PLAYER_ID, Movement_PlayerEnterLab)
    waitmovement(PLAYER_ID)
    closedoor(40, 16)
	waitdooranim
    warp(MAP_GRAVEL_TOWN_TIMS_LAB, 0, 0, 0)
}

movement Movement_TimStart1 {
    walk_up
    walk_left
    walk_up * 4
}

movement Movement_TimStart2 {
    walk_up
    walk_left
    walk_up
    walk_left
    walk_up * 3
}

movement Movement_TimStart3 {
    walk_up
    walk_left
    walk_up
    walk_left * 2
    walk_up * 3
}

movement Movement_TimStart4 {
    walk_up
    walk_left
    walk_up
    walk_left
    walk_up
    walk_left * 2
    walk_up * 2
}

movement Movement_TimStart5 {
    walk_up
    walk_left * 2
    walk_up * 2
    walk_left * 3
    walk_up * 2
}

movement Movement_TimSequenceBreak {
    walk_left * 3
    walk_up * 3
    delay_16 * 4
    face_left
    delay_16 * 3
    face_right
    delay_16 * 3
    face_down
    delay_16 * 2
    walk_up * 2
    face_right
    delay_16 * 2
    face_left
    delay_16
    emote_exclamation_mark
    delay_16 * 3
    walk_left
    delay_16 * 2
}

movement Movement_PlayerUndoSequenceBreakNear {
    walk_up * 4
    walk_right * 3
    walk_down * 4
    delay_16 * 2
}

movement Movement_PlayerUndoSequenceBreakFar {
    walk_up
    walk_right
    walk_up * 3
    walk_right * 3
    walk_down * 4
    delay_16 * 2
}

// He disapproves of your shennanigans.
movement Movement_TimStarePlayerSequenceBreakNear {
    delay_16 * 6
    face_up
}

movement Movement_TimStarePlayerSequenceBreakFar {
    delay_16 * 8
    face_up
}

movement Movement_TimWalkToLab1 {
    walk_down * 17
    walk_right * 12
    delay_8
    walk_in_place_fastest_up
    delay_16 * 3
    walk_up
    set_invisible
}

movement Movement_TimWalkToLab2 {
    walk_down * 17
    walk_right * 13
    delay_8
    walk_in_place_fastest_up
    delay_16 * 3
    walk_up
    set_invisible
}

movement Movement_TimWalkToLab3 {
    walk_down * 17
    walk_right * 14
    delay_8
    walk_in_place_fastest_up
    delay_16 * 3
    walk_up
    set_invisible
}

movement Movement_TimWalkToLab4 {
    walk_down * 12
    walk_right
    walk_down * 5
    walk_right * 14
    delay_8
    walk_in_place_fastest_up
    delay_16 * 3
    walk_up
    set_invisible
}

movement Movement_TimWalkToLab5 {
    walk_down * 12
    walk_right * 2
    walk_down * 5
    walk_right * 14
    delay_8
    walk_in_place_fastest_up
    delay_16 * 3
    walk_up
    set_invisible
}

movement Movement_TimEnterLab {
    emote_exclamation_mark
    walk_up
    set_invisible
}

movement Movement_PlayerWalkToLab1 {
    walk_down * 18
    walk_right * 11
}

movement Movement_PlayerWalkToLab2 {
    walk_down * 18
    walk_right * 12
}

movement Movement_PlayerWalkToLab3 {
    walk_down * 18
    walk_right * 13
}

movement Movement_PlayerWalkToLab4 {
    walk_down * 13
    walk_right
    walk_down * 5
    walk_right * 13
}

movement Movement_PlayerWalkToLab5 {
    walk_down * 13
    walk_right * 2
    walk_down * 5
    walk_right * 13
}

movement Movement_PlayerEnterLab {
    delay_16
    walk_right
    walk_up
    set_invisible
}
